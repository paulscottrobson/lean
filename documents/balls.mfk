import stdlib
import stdio
import random

import X16

void main() {
    //Need to stop things moving around to quickly
    word pause

    word ballCount
    array ballCountString[11]

    array BX[255]
    array BY[255]
    array BC[255]
    array DX[255]
    array DY[255]

    if (X_ModeGet() != 80) {
        X_ModeSwap()
    }




label GetBallCount
    //Clear the Screen
    V_SetArea(0,0,79,59,32,0)

    const array b = ["number of balls (1-255)?" cbmscr   ,0]
    V_PutString(30,30,60,30,123,0,b)

    const array allowChars = ["0123456789" cbmscr   ,0]
    G_GetStringLine(60,30,10,allowChars,ballCountString)

    //Get Ball Count
    ballCount = strz2word(ballCountString)

    if (ballCount > 255)
    {
        goto GetBallCount
    }

    //Clear the screen again
    V_SetArea(0,0,79,59,32,0)

    //Setup Starting Ball Positions
    byte lp
    for lp , 0 , to , ballCount {
        BX[lp] = U_RandBetween(0,79)
        BY[lp] = U_RandBetween(0,59)

        BC[lp] = U_RandBetween(0,255)

        DX[lp] = U_RandBetween(0,1) 
        DY[lp] = U_RandBetween(0,1) 
    }

    //Display Balls
    while (true)
    {
        //Loop though all balls clearing current spot and setting new spot
        for lp , 0 , to , ballCount {
            //Clear existing Location the ball is at
            vera_poke(BY[lp]*256+BX[lp]*2+1, 0)

            
            if DX[lp] == 0 {
                if (BX[lp] == 0) 
                {
                    DX[lp] = 1
                } else {
                    BX[lp]=BX[lp]-1
                }
            } else if DX[lp] == 1 {
                if (BX[lp] == 79) 
                {
                    BX[lp] = 78
                    DX[lp] = 0
                } else {
                    BX[lp]=BX[lp]+1
                }
            } 
            if DY[lp] == 0 {
                if (BY[lp] == 0) 
                {
                    DY[lp] = 1
                } else {
                    BY[lp]=BY[lp]-1
                }
            } else if DY[lp] == 1 {
                if (BY[lp] == 59) 
                {
                    BY[lp] = 58
                    DY[lp] = 0
                } else {
                    BY[lp]=BY[lp]+1
                }
            } 

            //Put the new ball possition
            vera_poke(BY[lp]*256+BX[lp]*2+1, BC[lp])
        }

        X_PauseCycle(4000)
    }
}


