word vera.low@$9f20
byte vera.high@$9f22
byte vera.data@$9f23

word x.ptr@$10,xi.ptr@$12,y.ptr@$14,yi.ptr@$16,col.ptr@$18

byte count
byte current
word seed
//
//			set write location to R
//
proc vera.set()
	R->S
	vera.low=R 						// write low address
	A=$10 vera.high = A 			// write high address
	S->R
endproc
//
//			write A to data port.
//
proc vera.write()
	vera.data=A
endproc
//
//			Random number generator
//
proc random()
	Y->S
	byte limit
	limit = R
	repeat
		R=seed R=? if R=$AB9E seed=R endif
		R=seed >>seed A&1 A=? if R=seed R*$B400 seed=R endif
		R=seed R.Swap A=R A-limit
	CC? until
	R=seed R.Swap A=R
	S->Y
endproc
//
//			Create ball Y
//
proc ball.create()
	random(80) x.ptr[y]=A
	random(60) y.ptr[y]=A
	A=1 xi.ptr[y]=A 
	A=1 yi.ptr[y]=A
	random(15) ++A col.ptr[y]=A
endproc
//
//			Draw ball R
//
proc ball.draw()
	A->S 
	A=y.ptr[y] R=A 
	R.swap A+x.ptr[y] A+x.ptr[y] vera.set()
	S->A vera.write()
	A=col.ptr[y] vera.write()
endproc	
//
//			Main Program
//
proc main()
	x.ptr=R 						// Set four pointers to point to 1/4k each.
	R+256 y.ptr=R 
	R+256 xi.ptr=R 	
	R+256 yi.ptr=R
	R+256 col.ptr=R

	vera.set(0) 					// Clear the screen.
	$1E00 r.for
		vera.write(32) vera.write(1)
	next

	A=112 count=a 					// Count of balls (no input ... yet)
	A=count a.for 					// Create the balls.
		Y=A
		ball.create()
		ball.draw(81)
	next
endproc

remove.locals
